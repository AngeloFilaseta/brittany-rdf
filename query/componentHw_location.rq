# Return the location of all the Hardware Component, following the chain [edge -> optional(plant) -> environment -> greenhouse].

PREFIX : <https://brittany-persistence-service.herokuapp.com/#>

SELECT DISTINCT ?greenhouse ?environment ?plant ?edge ?componentHw

WHERE {
    ?greenhouse :hasGreenhouseEdge ?edge .
    ?edge :collectComponentHw ?componentHw .
    OPTIONAL {
        ?plant :isPlantInEnvironment ?environment .
        ?edge :monitorPlant ?plant .
    }
    OPTIONAL {
        ?edge :monitorEnvironment ?environment.
    }
}

ORDER BY ?edge

# OUTPUT EXAMPLE

#-----------------------------------------------------------------------------------------------------------------
#| greenhouse          | environment             | plant         | edge          | componentHw                   |
#=================================================================================================================
#| :angelo-greenhouse  | :angelo-environment-one | :angelo-basil | :angelo-edge  | :angelo-digital-light-dx      |
#| :angelo-greenhouse  | :angelo-environment-one | :angelo-basil | :angelo-edge  | :angelo-digital-light-sx      |
#| :giorgia-greenhouse | :giorgia-environment    |               | :giorgia-edge | :giorgia-digital-light-center |
#-----------------------------------------------------------------------------------------------------------------
